string g_sApp = "Weld";

key g_kWelder;

integer CMD_OWNER = 500;
//integer CMD_TRUSTED = 501;
//integer CMD_GROUP = 502;
integer CMD_WEARER = 503;
//integer CMD_EVERYONE = 504;

integer NOTIFY = 1002;
integer NOTIFY_OWNERS=1003;
//integer REBOOT = -1000;

integer LM_SETTING_SAVE = 2000;


integer MENUNAME_REQUEST = 3000;
integer MENUNAME_RESPONSE = 3001;
//integer MENUNAME_REMOVE = 3003;

integer TIMEOUT_FIRED = 30499;

integer AUTH_REQUEST = 600;
integer AUTH_REPLY=601;

integer DIALOG = -9000;
integer DIALOG_RESPONSE = -9001;
integer DIALOG_TIMEOUT = -9002;
//string UPMENU = "BACK";
//string ALL = "ALL";



Dialog(key kAv, string sPrompt, list lChoices, list lUtilityButtons, integer iPage, integer iAuth, string sName) {
    
    if(sName == "weld"){
        sPrompt = "weldby:"+llLinksetDataRead("intern_weldby");
        if((integer)llLinksetDataRead("intern_weld")){
            sPrompt = "\n\n* This collar is Welded by secondlife:///app/agent/"+llLinksetDataRead("intern_weldby")+"/about *";
            sPrompt += "\n\nThe unwelder is located at\n[http://maps.secondlife.com/secondlife/KBar%20West/28/80/1201 OpenCollar headquarters]";
            lChoices = ["w̶e̶l̶d̶e̶d̶"];
            
        }else{
            sPrompt="\n\nThis app will let you weld the collar.\nWhich removes the ability to unlock.\n\n⚠ Proceed with caution ⚠\nWelding is semi-perminent and can only be undone with the use of an external unwelder.";

            if(!(integer)llLinksetDataRead("global_locked")){
                sPrompt += "\n\nThe collar is not locked yet.\nBefore you can weld, you must lock it first.";
                lChoices = ["Lock"];
            }else{
                lChoices = ["Weld Now"];
            }
        }
        lChoices += ["Help","BACK"];
    }
    
    llMessageLinked(LINK_SET, DIALOG, (string)kAv + "|" + sPrompt + "\n|"+(string)iPage+"|" + llDumpList2String(lChoices, "`") + "|" + llDumpList2String(lUtilityButtons, "`") + "|" + (string)iAuth, sName+"~"+llGetScriptName());
}

UserCommand(integer iAuth, string sStr, key kAv) {
    list lParameters = llParseString2List(sStr, [" "], []);
    string sChangetype = llList2String(lParameters,0);
    string sChangevalue = llList2String(lParameters,1);
    if(sChangetype == "menu"){
        if(sChangevalue == g_sApp){ // Weld button pressed
            Dialog(kAv,"",[],[],0,iAuth,"weld");
        }
    }
}

default
{
    state_entry(){
    }
    
    link_message(integer iSender,integer iNum,string sStr,key kID){
        if(iNum >= CMD_OWNER && iNum <= CMD_WEARER) {
            UserCommand(iNum, sStr, kID);
            
        }else if (iNum == MENUNAME_REQUEST  && sStr == "Apps") {
            //llLinksetDataWrite("Menu_Apps_"+g_sApp,"1");/*/
            llMessageLinked(LINK_SET, MENUNAME_RESPONSE, "Apps|"+g_sApp, "");//*/
            
        } else if(iNum == DIALOG_RESPONSE){          
            integer iPos = llSubStringIndex((string)kID, "~"+llGetScriptName());
            if(iPos>0){
                string sMenu = llGetSubString(kID, 0, iPos-1);
                list lMenuParams = llParseString2List(sStr, ["|"],[]);
                key kAv = llList2Key(lMenuParams,0);
                string sButton = llList2String(lMenuParams,1);
                integer iAuth = llList2Integer(lMenuParams,3);
                
                if(sMenu == "weld"){
                    if(sButton == "Weld Now"){
                        if(iAuth==CMD_OWNER){
                            if((integer)llLinksetDataRead("global_locked")){
                                g_kWelder = kAv;
                                                                
                                llMessageLinked(LINK_SET, NOTIFY, "1secondlife:///app/agent/"+(string)g_kWelder+"/about is attempting to weld the collar. Consent is required", kAv);
                                Dialog(llGetOwner(), "[WELD CONSENT REQUIRED]\n\nsecondlife:///app/agent/"+(string)kAv+"/about wants to weld your collar. If you agree, you may not be able to unweld it without the use of a plugin or a addon designed to break the weld. If you disagree with this action, press no.", ["Yes", "No"], [], 0, iAuth, "weld~consent");
                            } else {
                                llMessageLinked(LINK_SET,NOTIFY,"0You have to first lock the collar before you can weld it", kAv);
                            }
                        } else llMessageLinked(LINK_SET,NOTIFY,"0%NOACCESS% to welding", kAv);
                        
                    }else if(sButton == "w̶e̶l̶d̶e̶d̶"){
                        Dialog(kAv,"",[],[],0,iAuth,"weld");
                        
                    }else if(sButton == "Lock"){
                        llMessageLinked(LINK_SET, 0, "lock", kAv);
                        llSleep(1);
                        Dialog(kAv,"",[],[],0,iAuth,"weld");
                        
                    }else if(sButton == "Help"){
                        llLoadURL(kAv, "\n\nGo to page for the Weld and Unweld guide.\n\n", "https://opencollar.cc/docs/Weld");
                        
                    }else if(sButton == "BACK"){
                        llMessageLinked(LINK_SET, iAuth, "menu Apps", kAv);
                    }
                }else if(sMenu == "weld~consent"){
                    if(sButton == "No"){
                        llMessageLinked(LINK_SET, NOTIFY, "1%NOACCESS% to welding the collar.", g_kWelder);
                        llMessageLinked(LINK_SET, 0, "menu Weld", g_kWelder);
                        
                    } else {
                        // do weld
                        if(g_kWelder){
                            /*
                            llLinksetDataWrite("intern_weldby",g_kWelder);
                            llLinksetDataWrite("intern_weld","1");
                            /*/                            
                            llMessageLinked(LINK_SET, LM_SETTING_SAVE, "intern_weld=1", "");
                            llMessageLinked(LINK_SET, LM_SETTING_SAVE, "intern_weldby="+(string)g_kWelder, ""); 
                            //*/
                            llMessageLinked(LINK_SET, NOTIFY_OWNERS, "%WEARERNAME%'s collar has been welded", g_kWelder);
                            llMessageLinked(LINK_SET, NOTIFY, "1Weld completed", llGetOwner());
                            
                            llMessageLinked(LINK_SET, 0, "menu Weld", g_kWelder);
                        }
                    }
                    g_kWelder = NULL_KEY;
                }
            }
        }
    }
}
